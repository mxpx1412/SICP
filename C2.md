# 2 Building Abstractions with Data

+ Simple numerical data often insufficient for complex computing problems, 
more complex data needed. 
+ *Compound data*: building abstractions by combining data objects, key 
aspect of programming languages. 
+ Reasons to compound data:
  + Elevate conceptual level.
  + Increase modularity. 
  + Enhance expressive power. 
+ *Compound data object*: data objects formed by combining multiple data 
objects.
  + E.g. for motivation: computing of rational numbers:
    + Can think of as two integers, one numerator, one denominator. 
    + Computing rationals using two integers requires explicit tracking, 
    not manageable to do frequently. 
    + "Glueing together" two integers as one conceptual unit via compound 
    data is therefore desired. 
+ *Data abstraction*: separating the program parts representing data and the 
parts using data:
    + Easier to design, maintain, modify.
    + E.g. Organizing rationals as one unit also separates the computing of 
    rationals from the way we combine integers into rationals - modularity. 
+ Expressive power e.g. suppose want to represent the concept of 
*linear combination*:
  + E.g. \(ax + by\)
  + If arguments are only numbers: 
  ```scheme
  (define (linear-combination a b x y) 
    (+ (* a x) (* b y)))
  ```
  + If arguments can be **anything that the concept of addition and 
  multiplication are defined**:
  ```scheme
  (define (linear-combination a b x y) 
    (add (mul a x) (mul b y)))
  ```
    + It is irrelevant what `a`, `b`, `x`, `y` are, only that the general 
    procedures `add` and `mul` will perform appropriate computation on them. 
    + Also irrelevant how arguments represented in primitives. 
  + It is important our language can manipulate compound data directly. 
+ *Abstraction barriers* can be constructed using data abstraction.
+ Compound data needs "glue" - can be done w/o special "data" operation, just 
procedures.
+ *Closure*: the combiner of data objects should allow combination of both 
primitive data objects, **and** compound data objects. 
+ Compound data objects can serve as *conventional interfaces* for combining 
program modules in mix-and-match ways. 
+ *Symbolic expressions*: data whose elementary parts can be arbitrary 
symbols. 
+ Many ways given data structure can be represented with simpler objects, 
effects are non-trivial depending on choice. 
+ *Generic operations*: operations that handle many different types of data, 
to deal with data that may be represented differently by different parts 
of a program. 
  + Requires more powerful abstraction barriers than those from simple data 
  abstraction if modularity to be maintained. 
+ *Data directd programming*: technique allowing individual data 
representations to be designed separately and combined without modification. 
  + i.e. combined *additively*.

## 2.1 Introduction to Data Abstraction

+ Recall in procedural abstraction: procedure's primitive implementation and 
its usage can be separated. 
+ *Data abstraction*: separating compound data objects' usage from how it 
is constructed from more primitive objects. 
+ Programs using compound data should operate on "abstract data". Making 
minimum assumptions about the data. 
+ The "concrete" data representation should be defined independently from 
the programs that use it. 
+ *Selectors* and *constructors* interface between the program and the 
data object, implementing abstract data in terms of the concrete 
representation. 

### 2.1.1 Example: Arithmetic Operations for Rational Numbers

+ Without even considering concrete representation, we can already 
synthesize ways of using rational numbers by *wishful thinking*: 
  + Suppose: 
    + `(make-rat n d)` constructor that returns rational number with 
    integer numerator `n` and integer denominator `d`.
    + `(numer x)` selector that returns numerator of `x`.
    + `(denom x)` selector that returns denominator of `x`. 
  + With above procedures, can operate on rationals by following: 
  \begin{align}
  \frac{n_1}{d_1} + \frac{n_2}{d_2} &= \frac{n_1 d_2 + n_2 d_1}{d_1 d_2} \\
  \frac{n_1}{d_1} - \frac{n_2}{d_2} &= \frac{n_1 d_2 - n_2 d_1}{d_1 d_2} \\
  \frac{n_1}{d_1} \cdot \frac{n_2}{d_2} &= \frac{n_1 n_2}{d_1 d_2} \\
  \frac{n_1/d_1}{n_2/d_2} &= \frac{n_1 d_2}{d_1 n_2} \\
  \frac{n_1}{d_1} = \frac{n_2}{d_2} &\iff n_1 d_2 = n_2 d_1 
  \end{align}
  + [Procedures defined in scheme](./Book_Examples_Source/C2_1_1a.scm):
  ```scheme
  (define (add-rat x y) 
    (make-rat 
      (+ (* (numer x) (denom y)) (* (numer y) (denom x))) 
      (* (denom x) (denom y))))

  (define (sub-rat x y) 
    (make-rat 
      (- (* (numer x) (denom y)) (* (numer y) (denom x))) 
      (* (denom x) (denom y))))

  (define (mul-rat x y) 
    (make-rat 
      (* (numer x) (numer y)) 
      (* (denom x) (denom y))))

  (define (div-rat x y) 
    (make-rat 
      (* (numer x) (denom y)) 
      (* (denom x) (numer y))))

  (define (equal-rat x y) 
    (= 
      (* (numer x) (denom y)) 
      (* (numer y) (denom x))))
  ```

#### Pairs

+ *Pair* compound structure can be constructed with primitive procedure 
`cons`.
  + `cons`: takes two arguments, returns compound data object with two 
  arguments as parts.
  + Extract parts using `car` and `cdr`:
    + `car`: "Contents of Address part of Register".
    + `cdr`: (could-er) "Contents of Decrement part of Register". 
    + From IBM 704 implementation of Lisp, referencing "address" and 
    "decrement" parts of memory location. 
+ [Usage of `cons`, `car`, `cdr`](./Book_Examples_Source/C2_1_1b.scm):
```scheme
1 ]=> ; Starting point to build compound data for rationals:
(define x (cons 1 2))
;Value: x

1 ]=> (car x)
;Value: 1

1 ]=> (cdr x)
;Value: 2
```
+ Pairs can be formed from pairs using `cons`:
```scheme
1 ]=> ; Forming pairs from pairs:
(define a (cons 1 2))
;Value: a

1 ]=> (define b (cons 3 4))
;Value: b

1 ]=> (define c (cons a b))
;Value: c

1 ]=> (car (car c))
;Value: 1

1 ]=> (car (cdr c))
;Value: 3
```
+ Pairs can be named and manipulated like primitive data.
+ Single compound data primitive *pair* is only "glue" needed 
for many more complex data structures. 
+ *List structured* data: data objects constructed from pairs. 

#### Representing rational numbers

+ Given two integers as respective numerator and denominator, following 
can [represent rational numbers](./Book_Examples_Source/C2_1_1c.scm):

```scheme
; Representing rationals
(define (make-rat n d) (cons (n d)))
(define (numer x) (car x))
(define (denom x) (cdr x))

; Printing a rational
(define (print-rat x) 
  (newline) 
  (display (numer x))
  (display "/")
  (display (denom x)))
```
```scheme
1 ]=> ; Test calculations:
(define x (make-rat 1 2))
;Value: x

1 ]=> (define y (make-rat 3 4))
;Value: y

1 ]=> (print-rat (add-rat x y))
10/8
;Unspecified return value
```
+ [Reducing rationals to lowest terms](./Book_Examples_Source/C2_1_1d.scm):
```scheme
(define 
  (make-rat n d) 
  (let 
    ((divisor (gcd n d))) 
    (cons (/ n divisor) (/ d divisor))))
```
```scheme
1 ]=> ; Test calculations:
(define x (make-rat 1 2))
;Value: x

1 ]=> (numer x)
;Value: 1

1 ]=> (denom x)
;Value: 2

1 ]=> (define y (make-rat 3 4))
;Value: y

1 ]=> (print-rat (add-rat x y))
5/4
;Unspecified return value

1 ]=> (print-rat (sub-rat x y))
-1/4
;Unspecified return value

1 ]=> (print-rat (mul-rat x y))
3/8
;Unspecified return value

1 ]=> (print-rat (div-rat x y))
2/3
;Unspecified return value

1 ]=> (equal-rat x y)
;Value: #f

1 ]=> (equal-rat x (make-rat 2 4))
;Value: #t
```

### 2.1.2 Abstraction Barriers

+ Underlying idea of data abstraction: identify set of operations that 
expresses all manipulation of a data object type, then using those 
operations only to manipulate the data type. 

```
      ______________________________________
______| Programs that use rational numbers |______
      |____________________________________|
        Rational numbers in problem domain
      ______________________________________
______|         add-rat sub-rat ...        |______
      |____________________________________|
  Rational numbers as numerators and denominators 
      ______________________________________
______|     make-rat    numer    denom     |______
      |____________________________________|
             Rational numbers as pairs
      ______________________________________
______|       cons      car      cdr       |______
      |____________________________________|
           However pairs are implemented
```
+ *Abstraction barriers* isolate "levels" of the system:
  + E.g. in rational numbers, barriers as shown above. 
  + Barriers separate the ("higher up") programs that use the data abstraction 
  with the ("lower down") programs that implement the abstraction. 
  + Procedures at each level are interfaces that define the barriers and 
  connect the different levels. 
+ Advantages of abstraction barriers:
  + Complex data structures often can be implemented in many ways using more 
  primitive forms.
  + Implementations of complex data affects programs operating on it. 
  + Changing representation time-consuming, expensive unless dependence on 
  representation is limited by design using barriers. 
  + Constraining dependence also allows flexibility. If two representations 
  are debatable, decision can be deferred while the rest of the program 
  progresses. 
+ E.g. [alternative implementation](./Book_Examples_Source/C2_1_2.scm) 
of rational number constructor, selectors: 
  ```scheme
  (define (make-rat n d) (cons n d))

  ; Reduce rationals in selectors instead of constructor:
  (define (numer x) 
    (let 
      ((divisor (gcd (car x) (cdr x)))) 
      (/ (car x) divisor)))
  (define (denom x) 
    (let 
      ((divisor (gcd (car x) (cdr x)))) 
      (/ (cdr x) divisor)))
  ```
  + Reduction computed when accessing parts of a rational number. 
  + May be preferable if not accessing same rational many times, to 
  save time on constructor. 
  + Due to designed barriers, this change would not affect `add-rat`, 
  `sub-rat` etc. 

### 2.1.3 What is Meant by Data?

+ *Data* can be thought of as being defined by set of constructors and 
selectors, along with specific conditions that must be fulfilled by the 
procedures to make a valid representation. 
  + E.g. in rational number example, if the number `x` is constructed from 
  integers `n` and `d`, we must have `(/ (numer x) (denom x))` be the same 
  as `(/ n d)` for the rational representation to be valid. 
  + Formalization of idea difficult. Two approaches: 
    + Method of *abstract models*: "procedures plus conditions", and usually 
    defines new data objects in terms of previously defined types of data 
    objects. Assertions can be checked by rewriting in terms of assertions of 
    previous object types. 
    + *Algebraic specfication*: procedures regarded as elements of an 
    abstract algebraic system. Axioms corresponding to "conditions" specifies 
    the behaviour of the system. Abstract algebra is used to check assertions. 
  + POV can be applied to both high-level and low-level data. E.g. for pairs, 
  only thing needed to know is that if `z` is `(cons x y)`, then `(car z)` 
  is `x` and `(cdr z)` is `y`, fitting the procedures and conditions model. 
    + Any three procedures fulfilling the conditions can implement pairs. 
    + E.g. 
    ```scheme
    (define (cons-alt x y) 
      (define (dispatch m) 
        (cond  
          ((= m 0) x) 
          ((= m 1) y) 
          (else (error "Argument not 0 or 1: CONS" m))))
      dispatch)

    (define (car-alt z) (z 0))
    (define (cdr-alt z) (z 1))
    ```
    + Above example returns a procedure from `cons-alt`, and `car-alt` and 
    `cdr-alt` then calls the procedure with either `0` or `1` as arguments 
    respectively. The usage in practice would be the same as the regular 
    `cons`, `car` and `cdr`. 
    ```scheme
    1 ]=> (define water (cons-alt "hydrogen" "oxygen"))
    ;Value: water

    1 ]=> (car-alt water)
    ;Value: "hydrogen"

    1 ]=> (cdr-alt water)
    ;Value: "oxygen"```
    ```
    ```scheme
    1 ]=> (car-alt (cons-alt "west" "east"))
    ;Value: "west"

    1 ]=> (cdr-alt (cons-alt "north" "south"))
    ;Value: "south"
    ```
    + Implementation example above while obscure, is perfectly adequate as it 
    fits condition required of pairs. 
    + Also notice: ability to manipulate procedures are objects automatically 
    allows representation of compound data.
    + Procedural representation of data part of style of *message passing*. 
+ *Church numerals* define a positive integer as repeated composition of a 
function \(f\), where Church numeral \(C_n\) is \(f(\dots f(x))\), for a 
total of \(n\) compositions. 

### 2.1.4 Extended Exercise: Interval Arithmetic

+ Alyssa P. Hacker wants to provide numerical results with known precision 
given inputs that have known precision. 
+ E.g. resistors' resistances usually given with interval of known tolerance. 
+ The *parallel equivalent resistance* \(R_p\) of two resistors \(R_1\) and 
\(R_2\) are computed as:
\[ R_p = \frac{1}{1/R_1 + 1/R_2} \]
+ \(R_p\) will have some known precision based on \(R_1\), \(R_2\). 
+ Alyssa P. Hacker's initial interval objects:
  + Based on interval objects of two end points. 
  + Arithmetic with intervals devised by Alyssa are 
  [as follows](./Book_Examples_Source/C2_1_4.scm):
  ```scheme
  (define (add-interval x y) 
    (make-interval 
      (+ (lower-bound x) (lower-bound y)) 
      (+ (upper-bound x) (upper-bound y))))

  (define (mul-interval x y) 
    (let 
      ((p1 (* (lower-bound x) (lower-bound y))) 
      (p2 (* (lower-bound x) (upper-bound y))) 
      (p3 (* (upper-bound x) (lower-bound y))) 
      (p4 (* (upper-bound x) (upper-bound y)))) 
      (make-interval 
        (min p1 p2 p3 p4) 
        (max p1 p2 p3 p4))))

  (define (div-interval x y) 
    (mul-interval 
      x
      (make-interval 
        (/ 1.0 (upper-bound y)) 
        (/ 1.0 (lower-bound y)))))
  ```
  + The *width* of an interval is the difference of the upper and lower 
  bounds of the interval, divided by \(2\). I.e. if \(I = [x, y]\), 
  width \(w_I = \frac{y - x}{2}\). 
+ Ways to express *parallel equivalent resistance*:
\[ \frac{R_1 R_2}{R_1 + R_2} \]
\[\frac{1}{1/R_1 + 1/R_2}\]
+ Equivalent expressions for real-numbers may not be equivalent in 
interval arithmetic. 

## 2.2 Hierarchical Data and the Closure Property

+ *Box-and-Pointer notation*: each object represented as pointer to 
a box. Box containing a primitive show the primitive.
+ A pair in box-and-pointer notation is a double box with two points, 
one point points to `car` part, another to `cdr` part. 
+ E.g. box-and-pointer for pair `(cons 1 2)`.

```
-> | * | * | -> | 2 |
     |
     v
   | 1 |
```

+ Pairs can combine other pairs, and thus can be a universal building 
block for other data structures. 
+ E.g. using pairs to combine 1, 2, 3, 4:

```scheme
(cons (cons 1 2) (cons 3 4))
```
```
-> | * | * | -> | * | * | -> | 4 |
     |            |
     v            v
 | * | * |    | * | * | -> | * | * |
   |   |        |            |   |
   v   v        v            v   v
| 1 || 2 |    | 1 |       | 2 || 3 |
```

```scheme
(cons (cons 1 (cons 2 3)) 4)
```
```
-> | * | * | -> | 4 |
     |
     v
 | * | * | -> | * | * |
   |            |   |
   v            v   v
 | 1 |       | 2 || 3 |
```

+ *Closure property* ...
  + ... of `cons`: ability to create pairs whose elements are pairs. 
  + ... in general: an operation satisfies the closure property if results 
  from combining objects with that opeartion can be further combined with 
  that same operation. 
+ *Hierarchical structures*: structures made up of parts, which are themselves 
made up of parts. 
+ The closure property allows for hierarchical structures. 
+ As previously shown, compound procedures rely on closure property of 
procedures. 
+ Closure in compound data allows pairs to represent trees, sequences etc. 
+ Addendum:
  + "Closure" term comes from abstract algebra. Elements \(a \in A\) are 
  *closed* under operation \(f\) if \(f(a) \in A\). 
  + "Closure" also has alternate unrelated meaning in LISP community: 
  meaning implementation technique for representing procedures with 
  free variables. 
  + Means of combination satisfying closure is not necessarily present 
  in many popular languages:
    + Fortran and BASIC do not allow arrays whose elements are themselves 
    arrays. 
    + C and Pascal permits structure whose elements are structures, but 
    pointers must be manipulated explicitly, and each field of a structure 
    must only have elements of prespecified form. 
    + "General glue" such as `cons` often missing. 

### 2.2.1 Representing Sequences

+ *Sequence*: ordered collection of data objects. 
+ Chain of pairs can represent a sequence: `car` points to 
next item in chain, `cdr` is the next pair. Sequence terminates 
by ending with a distinguished value that is not a pair. 
+ E.g. following for 
\(\{1, 2, 3, 4\}\):
```
-> | * | * | -> | * | * | -> | * | * | -> | * | / |
     |            |            |            |
     v            v            v            v
   | 1 |        | 2 |        | 3 |        | 4 |
```
```scheme
(cons 1 
  (cons 2 
    (cons 3 
      (cons 4 ()))))
```
+ *List*: sequence of pairs formed from nested `cons`'es. 
+ Lists can be constructed by Scheme primitive `list`.
  + `(list <a1> <a2> <a3> <...> <an>)`
    + Equivalent to `(cons <a1> (cons <a2> (cons <a3> (<...> (cons an ())))))`
  + e.g. `(list 1 2 3 4)`
  + Note: in the text *list* is a chain of pairs terminated by an 
  end-of-list marker. In general, *list structures* are any data 
  structure made out of pairs. 
  + While lists are printed as `(<a1> <a2> <a3> <...> <an>)`, the printed 
  expression is not the same as `(list <a1> <...> <an>)`, evaluating 
  the printed `(<a1> <...> <an>)` would be like evaluating the procedure 
  `<a1>` on `<a2> <...> <an>` which is not the same as 
  `(list <a1> <...> <an>)`.
  + `car` selects the first item in a list while `cdr` selects the sub-list 
  with everything except the first item. 
  + Nested `car` and `cdr` can extract subsequent items in the list. 
  + `cons` with an item and a list creates another list, that is the same 
  as the input list except with an additional item in the beginning. 
  + *Empty list*: sequence with no elements, is the value of `()` used 
  to terminate chain of pairs
  + In the text the empty list is described as `nil`, in newer versions of 
  Scheme `nil` is no longer used in this way. 

```scheme
1 ]=> (define one-through-four (cons 1 (cons 2 (cons 3 (cons 4 ())))))
;Value: one-through-four

1 ]=> (car one-through-four)
;Value: 1

1 ]=> (car (cdr one-through-four))
;Value: 2

1 ]=> (cons 0 one-through-four)
;Value: (0 1 2 3 4)

1 ]=> (define one-through-four (list 1 2 3 4))
;Value: one-through-four

1 ]=> (car one-through-four)
;Value: 1

1 ]=> (car (cdr one-through-four))
;Value: 2

1 ]=> (cons 0 one-through-four)
;Value: (0 1 2 3 4)
```

#### List Operations

+ When pairs are used to represent sequence as list, the list can be 
`cdr`'ed down. 
+ List index conventionally start at \(0\). 
+ E.g. procedure `list-ref` to return \(n\)th element in list.
  + \(n = 0\) then return `car` of list. 
  + Else return \(n-1\)-st item of `cdr` of list. 

```scheme
(define (list-ref items n) 
  (if 
    (= n 0) 
    (car items) 
    (list-ref (cdr items) (- n 1))))
```
```scheme
1 ]=> (list-ref one-through-four 0)
;Value: 1

1 ]=> (list-ref one-through-four 2)
;Value: 3
```

+ Scheme primitive predicate `null?` tests if argument is empty list. 
```scheme
1 ]=> (null? ())
;Value: #t
```
+ Using `null` for recursive process that computes list length: 
```scheme
(define (len-list-recursive items) 
  (if 
    (null? items) 
    0
    (+ 1 (len-list-recursive (cdr items)))))
```
```scheme
1 ]=> (len-list-recursive one-through-four)
;Value: 4
```
+ Using `null` for iterative process that computes list lenght: 
```scheme
(define (len-list items) 
  (define (len-list-iter count iter-list) 
    (if (null? iter-list) count (len-list-iter (+ count 1) (cdr iter-list)))) 
  (len-list-iter 0 items))
```
```scheme
1 ]=> (len-list one-through-four)
;Value: 4
```
+ Procedures can "`cons` up" an answer list while "`cdr`'ing down" a list. 
+ `append` takes two list and combines their elements into a new list. 
Implemented as recursive process: gives `items-2` if `items-1` is empty, else 
appends `(cdr items-1)` and `items-2` then `cons` with `(car items-1)`. 

```scheme
(define (append-list items-1 items-2) 
  (if 
    (null? items-1) 
    items-2 
    (cons (car items-1) (append-list (cdr items-1) items-2))))
```
```scheme
1 ]=> (append-list one-through-four one-through-four)
;Value: (1 2 3 4 1 2 3 4)
```

+ *Dotted-tail notation*:  a way to define procedures with arbitrary number 
of arguments in Scheme. 
  + If procedure definition has a dot before the last parameter, then when 
  the procedure is called, the first parameters will have the first arguments 
  as values, but the final parameter will be a *list* of the remaining 
  arguments. 
  ```scheme
  (define (foo x y . z) <body>)
  ```
    + If we call `(foo 1 2 3 4)`, then in the body of `foo`, `x` will 
    be `1`, `y` will be `2`, and `z` is the list `(3 4)`.
    + Equivalent `foo` with `lambda`. 
      ```scheme
      (define foo (lambda (x y . z) <body>))
      ```
  ```scheme
  (define (bar . w) <body>)
  ```
    + If we call `(bar 1 2 3)`, then in the body of `bar`, `w` will be 
    the list `(1 2 3)`.
    + Equivalent `bar` with `lambda`.
      ```scheme
      (define bar (lambda w <body>)) 
      ```

#### Mapping over lists

+ `map` procedure (text): used to apply transformation to each element in a list:
  + `map` takes as arguments: 
    + A procedure with one argument. 
    + A list. 
  + `map` returns: 
    + A list of results by applying the input procedure to each element in 
    the input list. 
  + `map` from text: 
  ```scheme
  (define (map proc items) 
    (if (null? items) 
      () 
      (cons (proc (car items)) (map proc (cdr items)))))
  ```
  + e.g. `scale-list`
    ```scheme
    (define num-list (list 0 1 2 3 4))
    ```
    ```scheme
    (define (scale-list items factor) 
      (map (lambda (x) (* factor x)) items))
    ```
    ```scheme
    1 ]=> (scale-list num-list 2)
    ;Value: (0 2 4 6 8)
    ```
+ `map` (supplied by Scheme): 
  + More general than `map` shown in text.
  + `map` takes: 
    + A procedure with `n` arguments. 
    + A total of `n` lists. 
  + `map` returns: 
    + A list of results, where: 
      + The \(i\)th element of the result list is from applying the input 
      procedure to the \(i\)th elements of the input lists. 
    ```scheme
    (define c (list 1 1 1 1 1 1))
    (define x (list 0 1 2 3 4 5))
    (define x-squared (list 0 1 4 9 16 25))
    ```
    ```scheme
    1 ]=> (map + x-squared x c)
    ;Value: (1 3 7 13 21 31)
    ```
+ `map` establishes an abstraction barrier that isolates the implementation 
of a list transformation from the extraction and combination of the list 
elements. 
  + Flexibility in low-level details of sequence implementations. 
  + Preserves concept of operations transforming sequences. 
+ `for-each`: similar to `map`. However does not produce a list of results, 
but runs the input procedure on each element of the input list. 
  ```scheme
  (for-each 
    (lambda x (newline) (display x)) 
    (list 1 2 3))
  ```
  + Will print `1`, `2`, `3` on three lines. 

### 2.2.2 Hierarchical Structures

+ List representations of sequences generalizes to sequences whose 
elements may also be sequences. 

```scheme
1 ]=> (cons (list 1 2) (list 3 4))

;Value: ((1 2) 3 4)

1 ]=> (car (cons (list 1 2) (list 3 4)))

;Value: (1 2)

1 ]=> (cdr (cons (list 1 2) (list 3 4)))

;Value: (3 4)
```
```
((1 2) 3 4) -> | * | * | --------------> | * | * | -> | * | () |
                 |                         |            |
                 v                       | 3 |        | 4 |
      (1 2) -> | * | * | -> | * | () |
                 |            |
                 v            v
               | 1 |        | 2 |
```
+ *Trees*: another representation of sequences whose elements may also be 
sequences. Each element in the sequence is a branch, and if the element 
is a sequence, then it forms a subtree with subbranches. 
```
((1 2) 3 4)
|____________
|           |
(1 2)       (3 4)
|______     |______
|     |     |     |
1     2     3     4
```
+ Recursion is natural for processing trees. Operations on trees can be 
reduced to operations on branches, and subbranches etc. until reaching 
the leaves. 
+ `pair` primitive tests if its argument is a pair. Helps recursive 
procedures on trees.
+ Recursive procedure to count leaves in a tree: 
```scheme
(define (count-leaves tree) 
  (cond 
    ((null? tree) 0) 
    ((not (pair? tree)) 1) 
    (else 
      (+ 
        (count-leaves (car tree)) 
        (count-leaves (cdr tree))))))
```
  + Ordering of `cond` in `count-leaves` matters, `()` satisfy both of the 
  first two predicates. 
```scheme
1 ]=> (define x (cons (list 1 2) (list 3 4)))
;Value: x

1 ]=> (define y (list x x))
;Value: y

1 ]=> (length x)
;Value: 3

1 ]=> (length y)
;Value: 2

1 ]=> (count-leaves x)
;Value: 4

1 ]=> (count-leaves y)
;Value: 8
```

#### Mapping over trees

+ Mapping can be extended to trees.
+ [Procedure to scale a tree recursively](./Book_Examples_Source/C2_2_2b.scm): 
```scheme
; Recursive evolving procedure to scale leaves
(define (scale-tree tree factor) 
  (cond 
    ((null? tree) ()) 
    ((pair? tree) 
      (cons 
        (scale-tree (car tree) factor) 
        (scale-tree (cdr tree) factor))) 
    (else (* tree factor))))
```
+ [Procedure to scale a tree by mapping](./Book_Examples_Source/C2_2_2b.scm), 
regarding a tree as a sequence 
of sub-trees:
```scheme
; Mapping over tree to scale leaves
(define (scale-tree-map tree factor) 
  (map 
    (lambda (sub-tree) 
      (if (pair? sub-tree) 
        (scale-tree sub-tree factor) 
        (* sub-tree factor))) 
    tree))
```
```scheme
1 ]=> (define tree-example (list 1 (list 2 (list 3 4) 5) (list 6 7)))
;Value: tree-example

1 ]=> (scale-tree tree-example 10)
;Value: (10 (20 (30 40) 50) (60 70))

1 ]=> (scale-tree-map tree-example 10)
;Value: (10 (20 (30 40) 50) (60 70))
```
+ Combinations of sequence operations and recursion allow many tree 
operations.

### 2.2.3 Sequences as Conventional Interfaces

+ *Conventional interfaces*
+ Consider procedure to summing the squares of odd numbers in a tree: 
  ```scheme
  (define (sum-odd-squares tree)
    (cond 
      ((null? tree) 0)
      ((not (pair? tree)) 
        (if (odd? tree) (square tree) 0))
      (else 
        (+ (sum-odd-squares (car tree))
        (sum-odd-squares (cdr tree))))))
  ```
  ```scheme
  1 ]=> (define num-tree (list 1 2 '(3 4 5) 6 '(7 8) 9))
  ;Value: num-tree

  1 ]=> (sum-odd-squares num-tree)
  ;Value: 165
  ```
  + General steps for `sum-odd-squares`:
    + Enumerate leaves.
    + Filter leaves to select for odds.
    + Squares selected leaves.
    + Accumulate results with `+` starting with `0`. 
+ Consider procedure for listing the even Fibonacci numbers up to 
\(\mathrm{Fib}(n)\):
  ```scheme
  (define (even-fibs n)
    (define (next k)
      (if (> k n)
        () 
        (let ((f (fib k)))
          (if (even? f)
            (cons f (next (+ k 1)))
            (next (+ k 1))))))
    (next 0))
  ```
  ```scheme
  1 ]=> (even-fibs 7)
  ;Value: (0 2 8)
  ```
  + General steps for `even-fibs`:
    + Enumerates integer `k` from `0` to `n`.
    + Computes each `(fib k)`.
    + Filters and selects evens. 
    + Accumulates results with `cons`, starting with `()`. 
+ Can conceptualize as signals flowing through cascade of stages:
  + `sum-odd-squares`:
  ```
  | enumerate:  |   | filter: |   | map:   |   | accumulate: |
  | tree leaves |-->| odd?    |-->| square |-->| +, 0        |
  ```
  + `even-fibs`:
  ```
  | enumerate:  |   | map: |   | filter: |   | accumulate: |
  | integers    |-->| fib  |-->| even?   |-->| cons, ()    |

  ```
  + However since each stage's implementation is spread out in 
  parts for both procedures, the procedures do not have the 
  signal flow structure. 

#### Sequence Operations

+ Concentrating signals using lists allows programs to reflect signal 
flow structure. 
+ List operations can be used on signal lists to implement processing 
at each stage. 
+ *Filtering* a sequence to select wanted elements based on a given predicate: 
```scheme
(define (filter predicate sequence) 
  (cond 
    ((null? sequence) ()) 
    ((predicate (car sequence)) 
      (cons (car sequence) (filter predicate (cdr sequence))))
    (else (filter predicate (cdr sequence)))))
```
```scheme
1 ]=> (filter odd? (list 1 2 3 4 5))
;Value: (1 3 5)
```
+ *Accumulating* a sequence:
```scheme
(define (accumulate op initial sequence) 
  (if (null? sequence) 
    initial 
    (op 
      (car sequence) 
      (accumulate op initial (cdr sequence)))))
```
```scheme
1 ]=> (accumulate cons () '(1 2 3 4 5))
;Value: (1 2 3 4 5)
```
+ *Enumerating* an integer interval:
```scheme
(define (enumerate-integers i n) 
  (if (> i n) () (cons i (enumerate-integers (+ i 1) n))))
```
```scheme
1 ]=> (enumerate-integers 1 4)
;Value: (1 2 3 4)
```
+ *Enumerating* a tree:
```scheme
(define (enumerate-tree tree) 
  (cond 
    ((null? tree) ()) 
    ((not (pair? tree)) (list tree)) 
    (else 
      (append 
        (enumerate-tree (car tree)) 
        (enumerate-tree (cdr tree))))))
```
```scheme
1 ]=> (define num-tree (list 1 2 '(3 4 5) 6 (list 7 '(8 9))))
;Value: num-tree

1 ]=> (enumerate-tree num-tree)
;Value: (1 2 3 4 5 6 7 8 9)
```
+ Reformulated `sum-odd-squares`:
```scheme
(define (sum-odd-squares tree) 
  (accumulate + 0 (filter odd? (enumerate-tree tree))))
```
```scheme
1 ]=> (sum-odd-squares num-tree)
;Value: 165
```
+ Reformulated `even-fibs`:
```scheme
(define (even-fibs n) 
  (accumulate cons () (filter even? (map fib (enumerate-integers 0 n)))))
```
```scheme
1 ]=> (even-fibs 7)
;Value: (0 2 8)
```
+ Reformulated procedure above are more modular. Combining relatively 
simple pieces. 
+ Modularity can be encouraged by providing standard library and 
conventional interfaces for connecting components. 
+ Modularity controls complexity, increases reusability. 
+ Using similar signal flow structure to list squares of Fib numbers 
in `list-fib-squares`:
```scheme
(define (list-fib-squares n) 
  (accumulate 
    cons 
    () 
    (map square (map fib (enumerate-integers 0 n)))))
```
```scheme
1 ]=> (list-fib-squares 7)
;Value: (0 1 1 4 9 25 64 169)
```
+ Using similar signal flow structure to compute squares of odds in a 
sequence in `prod-squares-odds`:
```scheme
(define (prod-squares-odds sequence) 
  (accumulate * 1 (map square (filter odd? sequence))))
```
+ Taking salary of highest programmer: 
```scheme
(define (salary-highest-paid-programmer records) 
  (accumulate max 0 (map salary (filter programmer? records))))
```
+ In above examples, sequences are *conventional interfaces* to allow 
combination of processing modules. 
+ Uisng sequences uniformly to represent structures localized data 
structure dependencies to sequence operations. 
+ Allow experimentation of alternative representations but leave overall 
program designs intact. 
+ *Horner's Rule* for polynomials:
  + Can be proven as the optimal way of evaluating polynomial. 
  \[
    \sum_{i=0}^n a_i x^i = ( \dots (a_n x + a_{n-1}) x + \dots + a_1) x + a_0 
  \]

